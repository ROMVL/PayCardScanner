apply plugin: 'com.android.library'

ext {
    PUBLISH_GROUP_ID = 'io.github.romvl'
    PUBLISH_VERSION = '2.0.0'
    PUBLISH_ARTIFACT_ID = 'PayCardScanner'
    PUBLISH_DESCRIPTION = 'PayCardScanner Android SDK'
    PUBLISH_URL = 'https://github.com/ROMVL/PayCardScanner'
    PUBLISH_LICENSE_NAME = 'MIT License'
    PUBLISH_LICENSE_URL = 'https://github.com/ROMVL/PayCardScanner/blob/master/LICENSE.md'
    PUBLISH_DEVELOPER_ID = 'ROMVL'
    PUBLISH_DEVELOPER_NAME = 'Vladyslav Romanik'
    PUBLISH_DEVELOPER_EMAIL = 'romanik.vladislav@gmail.com'
    PUBLISH_SCM_CONNECTION = 'scm:git:github.com/ROMVL/PayCardScanner.git'
    PUBLISH_SCM_DEVELOPER_CONNECTION = 'scm:git:ssh://github.com/ROMVL/PayCardScanner.git'
    PUBLISH_SCM_URL = 'https://github.com/ROMVL/PayCardScanner/tree/master'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    testBuildType "profiling"

    defaultConfig {
        versionCode 2000
        versionName "2.0.0"

        minSdkVersion rootProject.ext.minSdkVersionSdk
        targetSdkVersion rootProject.ext.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                abiFilters 'armeabi-v7a', "arm64-v8a", /*"x86",*/ "x86_64"
                arguments '-DANDROID_STL=c++_shared'
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-customer.pro'
        }
        debug {
            jniDebuggable true
        }
        profiling {
            initWith debug
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=Release"//, '-DCMAKE_VERBOSE_MAKEFILE=1'
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    resourcePrefix "paycardscanner_"
    namespace 'paycardscanner.sdk'
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.ext.constraintlayoutVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.ext.appCompatVersion"
}